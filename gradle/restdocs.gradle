apply plugin: "org.asciidoctor.convert"
apply plugin: 'idea'

sourceSets {
    restdocs {
        java.srcDir 'src/restdocs/java'
        resources.srcDir 'src/restdocs/resources'
    }
}

idea {
    module {
        testSourceDirs += file('src/restdocs/java')
    }
}
configurations {
    // Only needed if restdocs sources are used across projects (e.g. awp-spring-boot-common could have commonly
    // used classes that are needed in dependent projects
    //restdocsOutput
    jsondoclet
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {

    // For restDocs configuration to make use of dependencies properly, need to add compile/runtime dependencies
    restdocsCompile sourceSets.main.output
    restdocsCompile sourceSets.test.output
    restdocsCompile configurations.compile
    restdocsCompile configurations.testCompile
    restdocsRuntime configurations.runtime
    restdocsRuntime configurations.testRuntime

    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.1.0.RELEASE'
    testCompile 'org.springframework.restdocs:spring-restdocs-core:1.1.0.RELEASE'

    // Only needed if restdocs sources are used across projects (e.g. awp-spring-boot-common could have commonly
    // used classes that are needed in dependent projects
    //restdocsOutput sourceSets.restdocs.output

    testCompile group: 'capital.scalable', name: 'spring-auto-restdocs-core', version: '1.0.8'
    jsondoclet group: 'capital.scalable', name: 'spring-auto-restdocs-json-doclet', version: '1.0.8'
}

// Bundle in the spring boot jar
jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

ext {
    snippetsDir = file("$buildDir/generated-snippets")
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
    options.memberLevel = JavadocMemberLevel.PACKAGE
}

task restdocs(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the restdocs tests to generate snippets.'

    maxHeapSize = '1024m'

    testClassesDir = sourceSets.restdocs.output.classesDir
    classpath = sourceSets.restdocs.runtimeClasspath

    mustRunAfter tasks.test
}

restdocs {
    outputs.dir snippetsDir

    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir

    dependsOn jsonDoclet
}

asciidoctor {
    sourceDir = file('src/restdocs/resources')
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn restdocs
}

check.dependsOn restdocs